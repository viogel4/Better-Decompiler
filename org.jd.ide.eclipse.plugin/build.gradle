apply plugin: 'java'
apply plugin: 'eclipse'

//outputDir是有效的，在命令行下使用gradlew build进行构建时，会使用此配置的目录bin
//但在eclipse中，使用Eclipse编译时，不会使用此配置，详见.classpath文件
sourceSets {
    main {
        java {
            srcDir 'src'
            outputDir = file('bin')
        }
    }
}

//此配置可能会不生效，在eclipse的Configure Build Path中，将"Allow output folders for source folders"取消勾选
eclipse.classpath.defaultOutputDir = file('bin')


repositories {
	jcenter()
}

dependencies {
	implementation 'org.jd:jd-core:1.1.3'
	
	compileOnly 'org.eclipse.core:org.eclipse.core.commands:3.6.0'	
	compileOnly 'org.eclipse.core:org.eclipse.core.resources:3.7.100'

	compileOnly('org.eclipse.jdt:org.eclipse.jdt.core:3.21.0') {
		exclude group:'org.eclipse.platform'
	}
	compileOnly('org.eclipse.jdt:org.eclipse.jdt.ui:3.21.0') {
		exclude group:'org.eclipse.birt.runtime'
		exclude group:'org.eclipse.emf'
		exclude group:'org.eclipse.jdt'
		exclude group:'org.eclipse.platform'
		exclude group:'com.ibm.icu'
	}
	compileOnly('org.eclipse.platform:org.eclipse.jface:3.19.0') {
		exclude group:'org.eclipse.platform'
	}
	compileOnly('org.eclipse.platform:org.eclipse.jface.text:3.16.200') {
		exclude group:'org.eclipse.platform'
	}
	compileOnly('org.eclipse.platform:org.eclipse.swt.win32.win32.x86_64:3.114.0') {
		exclude group:'org.eclipse.platform'
	}
	compileOnly('org.eclipse.platform:org.eclipse.ui.workbench:3.118.0') {
		exclude group:'org.eclipse.emf'
		exclude group:'org.eclipse.platform'
	}
	compileOnly('org.eclipse.platform:org.eclipse.ui.workbench.texteditor:3.14.100') {
		exclude group:'org.eclipse.platform'
	}
	compileOnly('org.eclipse.platform:org.eclipse.ui.ide:3.17.0') {
		exclude group:'org.eclipse.platform'
	}
	compileOnly('org.eclipse.platform:org.eclipse.ui.editors:3.13.100') {
		exclude group:'org.eclipse.platform'
	}
}

compileJava {
	sourceCompatibility = '1.8'
	targetCompatibility = '1.8'	
	source 'src'
}

jar {
	archiveFileName = archiveBaseName.get() + '_' + archiveVersion.get() + '.' + archiveExtension.get()
	manifest {
		from 'META-INF/MANIFEST.MF'
	}	
	from fileTree('.') {
		include 'icons/**'
		include 'about.ini'
		include 'plugin.xml'
	}
	into('lib') {
		from project.configurations.runtimeClasspath
	}
}
